apply plugin: 'java'
apply plugin: 'project-report'

apply plugin: 'eclipse'
apply plugin: 'idea'

apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'solid.principles'
version = '0.0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    // tapestry 5.4-alpha repository
    maven {
        url "https://repository.apache.org/content/groups/staging"
    }
    mavenLocal() // For tapestry snapshot release built and installed locally
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar
            artifact packageJavadoc
        }
    }
}

def tapestryVersion = '5.4.4'
def slf4jVersion = '1.7.25'

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion

    compile group: 'org.apache.tapestry', name: 'tapestry-core', version: tapestryVersion
}

jar {
    manifest {
        attributes ('Tapestry-Module-Classes': 'solid.principles.tapestry5.modules.Tapestry5ComponentsModule')
    }
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath

    options
    {
        setMemberLevel JavadocMemberLevel.PUBLIC

        links "https://docs.oracle.com/javase/8/docs/api/"
    }
}

task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

eclipse {
    classpath {
        downloadJavadoc = true
    }
}
